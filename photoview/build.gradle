apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.1.0"
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}


afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION

                pom.project {
                    name 'PhotoView'
                    packaging 'aar'
                    // optionally artifactId can be defined here
                    description 'mplementation of ImageView for Android that supports zooming, by various touch gestures.'
                    url 'https://github.com/muthuraj57/PhotoView'

                    scm {
                        url 'https://github.com/muthuraj57/PhotoView'
                        connection 'scm:git:https://github.com/muthuraj57/PhotoView.git'
                        developerConnection 'scm:git:git@github.com:muthuraj57/PhotoView'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'muthuraj57'
                            name 'Muthuraj'
                        }
                    }
                }
            }
        }
    }

    signing {
        sign configurations.archives
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.getByName("main").java.srcDirs
    }

    artifacts {
        archives sourcesJar
    }
}